buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
//        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

//plugins {
//  id "com.wynprice.cursemaven" version "2.1.1"
//}

apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

//apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = project.modVersion
group = 'architectspalette.core.ArchitectsPalette'
archivesBaseName = "${project.modName}-${project.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
}


//Add datagen and asset repo to resources folder when built
sourceSets.main.resources {
    srcDirs += 'src/ap-assets/resources'
    srcDirs += 'src/generated/resources'
}

minecraft {
    mappings channel: "${project.mappingsChannel}", version: project.mappings
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            arg "--mixin="+project.modId+".mixins.json"

            mods {
                architects_palette {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            arg "--mixin="+project.modId+".mixins.json"

            mods {
                architects_palette {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', project.modId, '--all', '--output', file('src/generated/resources/'), '--existing', file("src/main/resources"), '--existing', file("src/ap-assets/resources")

            environment 'target', 'fmluserdevdata'

            mods {
                architects_palette {
                    source sourceSets.main
                }
            }
        }
    }
}


//When duplicate files are found, keep ones from main, exclude copies
processResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

//JEI repositories
repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"
//    implementation fg.deobf("curse.maven:jei:${project.jei}")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

    //Copied from https://github.com/mezz/JustEnoughItems/wiki/Getting-Started-%5B1.13-and-Up%5D
    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-${minecraftVersion}:${jei_version}:api")
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraftVersion}:${jei_version}")

}

jar {
    //Added duplicate strategy here too, not sure if it's required
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes([
            "Specification-Title": "Architects Palette",
            "Specification-Vendor": "Architects Palette",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Architects Palette",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "${modId}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')